#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает одноразовый пароль авторизации чата в разрезе бота и сценария.
// 
// Параметры:
//  Сценарий - СправочникСсылка.УБТ_СценарииБотовTelegram - Сценарий
// 
// Возвращаемое значение:
//  Строка - Пароль 
Функция ОдноразовыйПарольАвторизацииЧата(Сценарий) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.Пароль,
						  |	УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.ДействителенДо
						  |ИЗ
						  |	РегистрСведений.УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram КАК УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram
						  |ГДЕ
						  |	УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.Сценарий = &Сценарий
						  |	И УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.ДействителенДо > &ТекущаяДатаСеанса");
	Запрос.УстановитьПараметр("Сценарий", Сценарий);
	Запрос.УстановитьПараметр("ТекущаяДатаСеанса", ТекущаяДатаСеанса());

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Возврат Новый Структура("Пароль, ДействителенДо", Выборка.Пароль, Выборка.ДействителенДо);
	КонецЕсли;

	Возврат СгенерироватьНовыйОдноразовыйПароль(Сценарий);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СгенерироватьНовыйОдноразовыйПароль(Сценарий)

	ПолныйПароль = СлучайнаяСтрока(12);

	Пароль = СтрШаблон("%1 %2 %3", Лев(ПолныйПароль, 4), Сред(ПолныйПароль, 4, 4), Прав(ПолныйПароль, 4));

	СрокДействияПароляВСекундах = 60;
	ДействителенДо = ТекущаяДатаСеанса() + СрокДействияПароляВСекундах;

	НоваяЗапись = СоздатьМенеджерЗаписи();
	НоваяЗапись.Сценарий = Сценарий;
	НоваяЗапись.Пароль = Пароль;
	НоваяЗапись.ДействителенДо = ДействителенДо;
	НоваяЗапись.Записать();

	Возврат Новый Структура("Пароль, ДействителенДо", Пароль, ДействителенДо);

КонецФункции

Функция СлучайнаяСтрока(Длина)

	ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	СлучайнаяСтрока = "";

	Для Сч = 1 По Длина Цикл
		Набор = ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 3);
		Если Набор = 1 Тогда
			СлучайныйСимвол = ГенераторСлучайныхЧисел.СлучайноеЧисло(0, 9);
		ИначеЕсли Набор = 2 Тогда
			СлучайныйСимвол = Символ(ГенераторСлучайныхЧисел.СлучайноеЧисло(65, 90));
		ИначеЕсли Набор = 3 Тогда
			СлучайныйСимвол = Символ(ГенераторСлучайныхЧисел.СлучайноеЧисло(97, 122));
		КонецЕсли;
		СлучайнаяСтрока = СлучайнаяСтрока + СлучайныйСимвол;
	КонецЦикла;

	Возврат СлучайнаяСтрока;

КонецФункции

#КонецОбласти

#КонецЕсли