#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("Сценарий", Сценарий);

	ОбновитьОдноразовыйПарольНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПодключитьОбработчикОжидания("ОбновитьОдноразовыйПароль", 1);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкопироватьПароль(Команда)

	СкопироватьПарольВБуферОбмена();

	ТекстСообщения = НСтр(
		"az = 'Parol mübadilə buferinə kopyalandı';en = 'The OTP has been copied to the clipboard';ru = 'Пароль скопирован в буфер обмена.'");
	ПоказатьОповещениеПользователя( , , ТекстСообщения, БиблиотекаКартинок.Успешно32);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОдноразовыйПароль()

	ОбновитьОдноразовыйПарольНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьОдноразовыйПарольНаСервере()

	Если ЗначениеЗаполнено(ДействителенДо) И ТекущаяДатаСеанса() < ДействителенДо Тогда
		Возврат;
	КонецЕсли;

	ДанныеПароля = РегистрыСведений.УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.ОдноразовыйПарольАвторизацииЧата(
		Сценарий);

	Пароль = ДанныеПароля.Пароль;
	ДействителенДо = ДанныеПароля.ДействителенДо;

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПарольВБуферОбмена()

	HTMLДокумент = Элементы.БуферОбмена.document;
	ВременноеПолеВвода = HTMLДокумент.createElement("INPUT");
	АктивныйЭлемент = HTMLДокумент.activeElement;
	ВременноеПолеВвода.value = Пароль;
	HTMLДокумент.body.appendChild(ВременноеПолеВвода);
	ВременноеПолеВвода.select();
	HTMLДокумент.execCommand("copy");
	HTMLДокумент.body.removeChild(ВременноеПолеВвода);
	АктивныйЭлемент.focus();

КонецПроцедуры

#КонецОбласти