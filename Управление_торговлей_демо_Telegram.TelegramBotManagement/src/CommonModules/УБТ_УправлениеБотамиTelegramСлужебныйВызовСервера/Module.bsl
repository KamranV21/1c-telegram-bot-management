#Область ПрограммныйИнтерфейс

// Преобразует сценарий бота Telegram в JSON схему.
// 
// Параметры:
//  Сценарий - СправочникСсылка.УБТ_СценарииБотовTelegram - Сценарий
// 
// Возвращаемое значение:
//  Строка - Адрес двоичных данных JSON-файла
Функция СохранитьJSONСхемуСценарияБотаTelegram(Сценарий) Экспорт

	СхемаСценария = СхемаСценарияБотаTelegram(Сценарий);

	JSON = УБТ_РаботаСJSON.ЗначениеВJSON(СхемаСценария);

	Возврат ПоместитьВоВременноеХранилище(JSON, Новый УникальныйИдентификатор);

КонецФункции

// Загружает сценарий бота Telegram из файла JSON.
// 
// Параметры:
//  СценарийОбъект - СправочникОбъект.УБТ_СценарииБотовTelegram - Данные сценария
//  АдресJSONСхемы - Строка - Адрес JSON-схемы во временном хранилище
// 
Процедура ЗаполнитьСценарийБотаTelegramПоJSONСхеме(СценарийОбъект, АдресJSONСхемы) Экспорт

	JSON = СодержимоеФайлаИзАдресаВоВременномХранилище(АдресJSONСхемы);

	СхемаСценария = УБТ_РаботаСJSON.JSONВЗначение(JSON, , Ложь); // см. НоваяСтруктураСхемыСценарияБотаTelegram

	Если Не ЗначениеЗаполнено(СценарийОбъект.Бот) Тогда
		АктивныеБотыTelegram = Справочники.УБТ_БотыTelegram.АктивныеБотыTelegram();
		Если АктивныеБотыTelegram.Количество() > 0 Тогда
			СценарийОбъект.Бот = АктивныеБотыTelegram[0];
		КонецЕсли;
	КонецЕсли;

	СценарийОбъект.Отчеты.Очистить();
	СценарийОбъект.РасширенияОтчетов.Очистить();
	СценарийОбъект.Этапы.Очистить();
	СценарийОбъект.КнопкиКлавиатуры.Очистить();
	СценарийОбъект.Чаты.Очистить();

	ЗаполнитьЗначенияСвойств(СценарийОбъект, СхемаСценария);

	СценарийОбъект.ТипСценария = ?(ЗначениеЗаполнено(СхемаСценария.ТипСценария),
		Перечисления.УБТ_ТипыСценариевБотовTelegram[СхемаСценария.ТипСценария],
		Перечисления.УБТ_ТипыСценариевБотовTelegram.ПустаяСсылка());

	Для Каждого Строка Из СхемаСценария.Отчеты Цикл
		НоваяСтрока = СценарийОбъект.Отчеты.Добавить();
		ИдентификаторОтчета = Новый УникальныйИдентификатор(Строка.Отчет);
		НоваяСтрока.Отчет = Справочники.ВариантыОтчетов.ПолучитьСсылку(ИдентификаторОтчета);
		НоваяСтрока.УникальныйИдентификатор = Новый УникальныйИдентификатор(Строка.УникальныйИдентификатор);
	КонецЦикла;

	Для Каждого Строка Из СхемаСценария.РасширенияОтчетов Цикл
		НоваяСтрока = СценарийОбъект.РасширенияОтчетов.Добавить();
		НоваяСтрока.Расширение = Строка.Расширение;
		НоваяСтрока.УникальныйИдентификатор = Новый УникальныйИдентификатор(Строка.УникальныйИдентификатор);
	КонецЦикла;

	Для Каждого Строка Из СхемаСценария.Этапы Цикл
		НоваяСтрока = СценарийОбъект.Этапы.Добавить();
		НоваяСтрока.НаименованиеЭтапа = Строка.НаименованиеЭтапа;
		НоваяСтрока.ПроизвольныйКод = Строка.ПроизвольныйКод;
		НоваяСтрока.УникальныйИдентификатор = Новый УникальныйИдентификатор(Строка.УникальныйИдентификатор);
	КонецЦикла;

	Для Каждого Строка Из СхемаСценария.КнопкиКлавиатуры Цикл
		НоваяСтрока = СценарийОбъект.КнопкиКлавиатуры.Добавить();
		НоваяСтрока.Наименование = Строка.Наименование;
		НоваяСтрока.ЗапросНомераТелефона = Строка.ЗапросНомераТелефона;
		НоваяСтрока.УникальныйИдентификатор = Новый УникальныйИдентификатор(Строка.УникальныйИдентификатор);
	КонецЦикла;

	ВыборкаЧатов = Справочники.УБТ_ЧатыTelegram.ЧатыПоИдентификаторам(СценарийОбъект.Бот, СхемаСценария.Чаты).Выбрать();
	Пока ВыборкаЧатов.Следующий() Цикл
		НоваяСтрока = СценарийОбъект.Чаты.Добавить();
		НоваяСтрока.Чат = ВыборкаЧатов.Чат;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СхемаСценарияБотаTelegram(Сценарий)

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	УБТ_СценарииБотовTelegram.Наименование КАК Наименование,
						  |	УБТ_СценарииБотовTelegram.ТипСценария КАК ТипСценария,
						  |	УБТ_СценарииБотовTelegram.ПроизвольныйКод КАК ПроизвольныйКод,
						  |	УБТ_СценарииБотовTelegram.ТребуетсяАвторизация КАК ТребуетсяАвторизация,
						  |	УБТ_СценарииБотовTelegram.ИспользоватьРегламентноеЗадание КАК ИспользоватьРегламентноеЗадание,
						  |	УБТ_СценарииБотовTelegram.ИндивидуальноеИсполнениеСценария КАК ИндивидуальноеИсполнениеСценария,
						  |	УБТ_СценарииБотовTelegram.Отчеты.(
						  |		Отчет,
						  |		УникальныйИдентификатор) КАК Отчеты,
						  |	УБТ_СценарииБотовTelegram.РасширенияОтчетов.(
						  |		Расширение,
						  |		УникальныйИдентификатор) КАК РасширенияОтчетов,
						  |	УБТ_СценарииБотовTelegram.Этапы.(
						  |		НаименованиеЭтапа,
						  |		ПроизвольныйКод,
						  |		УникальныйИдентификатор) КАК Этапы,
						  |	УБТ_СценарииБотовTelegram.КнопкиКлавиатуры.(
						  |		Наименование,
						  |		ЗапросНомераТелефона,
						  |		УникальныйИдентификатор) КАК КнопкиКлавиатуры,
						  |	УБТ_СценарииБотовTelegram.Чаты.(
						  |		Чат.Код КАК ИдентификаторЧата) КАК Чаты
						  |ИЗ
						  |	Справочник.УБТ_СценарииБотовTelegram КАК УБТ_СценарииБотовTelegram
						  |ГДЕ
						  |	УБТ_СценарииБотовTelegram.Ссылка = &Сценарий");
	Запрос.УстановитьПараметр("Сценарий", Сценарий);

	ВыборкаШапка = Запрос.Выполнить().Выбрать();

	ВыборкаШапка.Следующий();

	СхемаСценария = НоваяСтруктураСхемыСценарияБотаTelegram();
	ЗаполнитьЗначенияСвойств(СхемаСценария, ВыборкаШапка, , "Отчеты, РасширенияОтчетов, Этапы, КнопкиКлавиатуры, Чаты");

	СхемаСценария.ТипСценария = ОбщегоНазначения.ИмяЗначенияПеречисления(ВыборкаШапка.ТипСценария);

	ВыборкаОтчеты = ВыборкаШапка.Отчеты.Выбрать();
	Пока ВыборкаОтчеты.Следующий() Цикл
		СхемаОтчетаСценария = НоваяСтруктураСхемыОтчетаСценарияБотаTelegram();
		СхемаОтчетаСценария.Отчет = Строка(ВыборкаОтчеты.Отчет.УникальныйИдентификатор());
		СхемаОтчетаСценария.УникальныйИдентификатор = Строка(ВыборкаОтчеты.УникальныйИдентификатор);
		СхемаСценария.Отчеты.Добавить(СхемаОтчетаСценария);
	КонецЦикла;

	ВыборкаРасширенияОтчетов = ВыборкаШапка.РасширенияОтчетов.Выбрать();
	Пока ВыборкаРасширенияОтчетов.Следующий() Цикл
		СхемаРасширенияОтчетаСценария = НоваяСтруктураСхемыРасширенияОтчетаСценарияБотаTelegram();
		ЗаполнитьЗначенияСвойств(СхемаРасширенияОтчетаСценария, ВыборкаРасширенияОтчетов);
		СхемаРасширенияОтчетаСценария.УникальныйИдентификатор = Строка(
			СхемаРасширенияОтчетаСценария.УникальныйИдентификатор);
		СхемаСценария.РасширенияОтчетов.Добавить(СхемаРасширенияОтчетаСценария);
	КонецЦикла;

	ВыборкаЭтапы = ВыборкаШапка.Этапы.Выбрать();
	Пока ВыборкаЭтапы.Следующий() Цикл
		СхемаЭтапаСценария = НоваяСтруктураСхемыЭтапаСценарияБотаTelegram();
		ЗаполнитьЗначенияСвойств(СхемаЭтапаСценария, ВыборкаЭтапы);
		СхемаЭтапаСценария.УникальныйИдентификатор = Строка(СхемаЭтапаСценария.УникальныйИдентификатор);
		СхемаСценария.Этапы.Добавить(СхемаЭтапаСценария);
	КонецЦикла;

	ВыборкаКнопкиКлавиатуры = ВыборкаШапка.КнопкиКлавиатуры.Выбрать();
	Пока ВыборкаКнопкиКлавиатуры.Следующий() Цикл
		СхемаКнопкиКлавиатурыСценария = НоваяСтруктураСхемыКнопкиКлавиатурыБотаTelegram();
		ЗаполнитьЗначенияСвойств(СхемаКнопкиКлавиатурыСценария, ВыборкаКнопкиКлавиатуры);
		СхемаКнопкиКлавиатурыСценария.УникальныйИдентификатор = Строка(
			СхемаКнопкиКлавиатурыСценария.УникальныйИдентификатор);
		СхемаСценария.КнопкиКлавиатуры.Добавить(СхемаКнопкиКлавиатурыСценария);
	КонецЦикла;

	ВыборкаЧаты = ВыборкаШапка.Чаты.Выбрать();
	Пока ВыборкаЧаты.Следующий() Цикл
		СхемаСценария.Чаты.Добавить(ВыборкаЧаты.ИдентификаторЧата);
	КонецЦикла;

	Возврат СхемаСценария

КонецФункции

// Новая структура схемы сценария бота telegram.
// 
// Возвращаемое значение:
//  Структура - Новая структура схемы сценария бота telegram:
// * Наименование - Строка
// * ТипСценария - Строка
// * ПроизвольныйКод - Строка
// * ТребуетсяАвторизация - Булево
// * ИспользоватьРегламентноеЗадание - Булево
// * ИндивидуальноеИсполнениеСценария - Булево
// * Отчеты - Массив Из Структура: см. НоваяСтруктураСхемыОтчетаСценарияБотаTelegram
// * РасширенияОтчетов - Массив Из Структура: см. НоваяСтруктураСхемыРасширенияОтчетаСценарияБотаTelegram
// * Этапы - Массив Из Структура: см. НоваяСтруктураСхемыЭтапаСценарияБотаTelegram
// * КнопкиКлавиатуры - Массив Из Структура: см. НоваяСтруктураСхемыКнопкиКлавиатурыБотаTelegram
// * Чаты - Массив Из Строка
Функция НоваяСтруктураСхемыСценарияБотаTelegram()

	СхемаСценария = Новый Структура;
	СхемаСценария.Вставить("Наименование", "");
	СхемаСценария.Вставить("ТипСценария", "");
	СхемаСценария.Вставить("ПроизвольныйКод", "");
	СхемаСценария.Вставить("ТребуетсяАвторизация", Ложь);
	СхемаСценария.Вставить("ИспользоватьРегламентноеЗадание", Ложь);
	СхемаСценария.Вставить("ИндивидуальноеИсполнениеСценария", Ложь);

	СхемаСценария.Вставить("Отчеты", Новый Массив);
	СхемаСценария.Вставить("РасширенияОтчетов", Новый Массив);
	СхемаСценария.Вставить("Этапы", Новый Массив);
	СхемаСценария.Вставить("КнопкиКлавиатуры", Новый Массив);
	СхемаСценария.Вставить("Чаты", Новый Массив);

	Возврат СхемаСценария;

КонецФункции

Функция НоваяСтруктураСхемыОтчетаСценарияБотаTelegram()

	СхемаОтчетаСценария = Новый Структура;
	СхемаОтчетаСценария.Вставить("Отчет", "");
	СхемаОтчетаСценария.Вставить("УникальныйИдентификатор", "");

	Возврат СхемаОтчетаСценария;

КонецФункции

Функция НоваяСтруктураСхемыРасширенияОтчетаСценарияБотаTelegram()

	СхемаРасширенияОтчетаСценария = Новый Структура;
	СхемаРасширенияОтчетаСценария.Вставить("Расширение", "");
	СхемаРасширенияОтчетаСценария.Вставить("УникальныйИдентификатор", "");

	Возврат СхемаРасширенияОтчетаСценария;

КонецФункции

Функция НоваяСтруктураСхемыЭтапаСценарияБотаTelegram()

	СхемаЭтапаСценария = Новый Структура;
	СхемаЭтапаСценария.Вставить("НаименованиеЭтапа", "");
	СхемаЭтапаСценария.Вставить("ПроизвольныйКод", "");
	СхемаЭтапаСценария.Вставить("УникальныйИдентификатор", "");

	Возврат СхемаЭтапаСценария;

КонецФункции

Функция НоваяСтруктураСхемыКнопкиКлавиатурыБотаTelegram()

	СхемаКнопкиКлавиатурыСценария = Новый Структура;
	СхемаКнопкиКлавиатурыСценария.Вставить("Наименование", "");
	СхемаКнопкиКлавиатурыСценария.Вставить("ЗапросНомераТелефона", Ложь);
	СхемаКнопкиКлавиатурыСценария.Вставить("УникальныйИдентификатор", "");

	Возврат СхемаКнопкиКлавиатурыСценария;

КонецФункции

Функция СодержимоеФайлаИзАдресаВоВременномХранилище(АдресФайлаВоВременномХранилище)

	ПутьКФайлу = ПолучитьИмяВременногоФайла("json");

	ДанныеИзХранилища = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);
	Если ТипЗнч(ДанныеИзХранилища) = Тип("ДвоичныеДанные") Тогда
		ДанныеИзХранилища.Записать(ПутьКФайлу);
	КонецЕсли;
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ПутьКФайлу);
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	УдалитьФайлы(ПутьКФайлу);

	Возврат Текст;

КонецФункции

#КонецОбласти