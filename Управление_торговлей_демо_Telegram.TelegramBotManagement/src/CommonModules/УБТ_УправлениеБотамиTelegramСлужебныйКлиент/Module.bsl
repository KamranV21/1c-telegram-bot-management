#Область ПрограммныйИнтерфейс

// Выгружает сценарий Telegram бота в файл JSON.
// 
// Параметры:
//  Сценарий - СправочникСсылка.УБТ_СценарииБотовTelegram - Сценарий
Процедура ВыгрузитьСценарийБотаTelegramВФайл(Сценарий) Экспорт

	АдресФайлаВоВременномХранилище = УБТ_УправлениеБотамиTelegramСлужебныйВызовСервера.СохранитьJSONСхемуСценарияБотаTelegram(
		Сценарий);

	Если Не ПустаяСтрока(АдресФайлаВоВременномХранилище) Тогда

		ИмяФайла = Строка(Сценарий) + ".json";
		НачатьПолучениеФайлаССервера(АдресФайлаВоВременномХранилище, ИмяФайла);

	КонецЕсли;

КонецПроцедуры

// Загружает сценарий бота Telegram из файла JSON.
// 
// Параметры:
//  ОписаниеОповещенияОЗавершении - ОписаниеОповещения - Описание оповещения о завершении
Процедура ЗагрузитьСценарийБотаTelegramИзФайла(ОписаниеОповещенияОЗавершении) Экспорт

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Заголовок = НСтр("az = 'Faylı seçin';en = 'Select file';ru = 'Выберите файл'");
	ПараметрыДиалога.Фильтр = НСтр("az = 'JSON';en = 'JSON';ru = 'JSON'") + " (*.json)|*.json|";

	НачатьПомещениеФайлаНаСервер(ОписаниеОповещенияОЗавершении, , , , ПараметрыДиалога, Новый УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти