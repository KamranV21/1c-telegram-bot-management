#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает GUID бота с префиксом расширения. Используется для совместимости с конфигурациями и платформами нижних версий.
// 
// Параметры:
//  Бот - СправочникСсылка.TBM_БотыTelegram - Бот
// 
// Возвращаемое значение:
//  Строка - Идентификатор элемента бота с префиксом
Функция ИдентификаторЭлементаБотаСПрефиксом(Бот) Экспорт

	Возврат "TBM_Бот_" + Строка(Бот.УникальныйИдентификатор());

КонецФункции

// Возвращает бота Telegram по имени.
// 
// Параметры:
//  ИмяБота - Строка - Имя бота
// 
// Возвращаемое значение:
//  СправочникСсылка.TBM_БотыTelegram - Бот Telegram
Функция НайтиБотаTelegramПоИмени(ИмяБота) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	TBM_БотыTelegram.Ссылка КАК Бот
						  |ИЗ
						  |	Справочник.TBM_БотыTelegram КАК TBM_БотыTelegram
						  |ГДЕ
						  |	TBM_БотыTelegram.Код = &ИмяБота
						  |	И НЕ TBM_БотыTelegram.ПометкаУдаления");
	Запрос.УстановитьПараметр("ИмяБота", ИмяБота);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Бот;
	КонецЕсли;

	Возврат ПустаяСсылка();

КонецФункции

// Возвращает бота Telegram по секретному заголовку.
// 
// Параметры:
//  СекретВебхука - Строка - Секрет бота
// 
// Возвращаемое значение:
//  СправочникСсылка.TBM_БотыTelegram - Бот Telegram
Функция НайтиБотаTelegramПоСекретномуЗаголовку(СекретВебхука) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	TBM_БотыTelegram.Ссылка КАК Бот
						  |ИЗ
						  |	Справочник.TBM_БотыTelegram КАК TBM_БотыTelegram
						  |ГДЕ
						  |	TBM_БотыTelegram.ВариантВзаимодействия = ЗНАЧЕНИЕ(Перечисление.TBM_ВариантыВзаимодействияСБотамиTelegram.Вебхук)
						  |	И TBM_БотыTelegram.СекретВебхука = &СекретВебхука
						  |	И НЕ TBM_БотыTelegram.ПометкаУдаления");
	Запрос.УстановитьПараметр("СекретВебхука", СекретВебхука);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Бот;
	КонецЕсли;

	Возврат ПустаяСсылка();

КонецФункции

#КонецОбласти

#КонецЕсли