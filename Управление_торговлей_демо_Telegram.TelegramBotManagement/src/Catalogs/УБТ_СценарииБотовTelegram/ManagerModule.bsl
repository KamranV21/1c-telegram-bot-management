#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Создает или обновляет псевдо-предопределенный сценарий авторизации чатов.
Процедура ИнициализироватьСценарийАвторизации() Экспорт

	АктуальнаяВерсияСценария = ВерсияСценарияАвторизации();

	СуществующийСценарийАвторизации = СценарийАвторизацииЧатов();

	Если ЗначениеЗаполнено(СуществующийСценарийАвторизации) Тогда
		ТекущаяВерсияСценария = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СуществующийСценарийАвторизации, "Версия");
		Если ТекущаяВерсияСценария >= АктуальнаяВерсияСценария Тогда
			Возврат;
		КонецЕсли;
		СценарийАвторизации = СуществующийСценарийАвторизации.ПолучитьОбъект();
		СценарийАвторизации.Этапы.Очистить();
		СценарийАвторизации.КнопкиКлавиатуры.Очистить();
	Иначе
		СценарийАвторизации = СоздатьЭлемент();
	КонецЕсли;

	СценарийАвторизации.Наименование = "Authorization";
	СценарийАвторизации.ТипСценария = Перечисления.УБТ_ТипыСценариевБотовTelegram.Общение;

	ЭтапОтказа = СценарийАвторизации.Этапы.Добавить();
	ЭтапОтказа.НаименованиеЭтапа = "Отказ в доступе";
	ЭтапОтказа.УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор);
	ЭтапОтказа.ПроизвольныйКод = "Если ТипЧата = Перечисления.УБТ_ТипыЧатовTelegram.Приватный Тогда
								 |	
								 |	Результат.Сообщение.Текст = ""Для продолжения необходимо авторизоваться. 
								 |	|
								 |	|Выберите один из вариантов авторизации: 
								 |	|
								 |	| - OTP: введите одноразовый пароль, сгенерированный на стороне учетной системы 
								 |	|
								 |	| - Номер телефона: отправьте свой номер телефона и дождитесь подтверждения со стороны администратора"";
								 |	
								 |	ЗавершитьЭтап(Параметры.НаименованиеТекущегоЭтапа);
								 |	
								 |Иначе
								 |	
								 |	Параметры.КнопкиКлавиатуры.Очистить();
								 |	
								 |	Результат.Сообщение.Текст = ""Для продолжения необходимо авторизоваться.
								 |	|
								 |	|Введите одноразовый пароль (OTP), сгенерированный на стороне учетной системы"";
								 |	
								 |	ЗавершитьЭтап(Параметры.НаименованиеТекущегоЭтапа);
								 |	ЗавершитьЭтап(""Проверка OTP / Сохранение номера телефона"");
								 |	
								 |КонецЕсли;";

	КнопкаОтправитьНомерТелефона = СценарийАвторизации.КнопкиКлавиатуры.Добавить();
	КнопкаОтправитьНомерТелефона.Наименование = "Отправить номер телефона";
	КнопкаОтправитьНомерТелефона.УникальныйИдентификатор = ЭтапОтказа.УникальныйИдентификатор;
	КнопкаОтправитьНомерТелефона.ЗапросНомераТелефона = Истина;

	КнопкаОтправитьНомерТелефона = СценарийАвторизации.КнопкиКлавиатуры.Добавить();
	КнопкаОтправитьНомерТелефона.Наименование = "Ввести одноразовый пароль (OTP)";
	КнопкаОтправитьНомерТелефона.УникальныйИдентификатор = ЭтапОтказа.УникальныйИдентификатор;

	ЭтапПроверки = СценарийАвторизации.Этапы.Добавить();
	ЭтапПроверки.НаименованиеЭтапа = "Проверка OTP / Сохранение номера телефона";
	ЭтапПроверки.УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор);
	ЭтапПроверки.ПроизвольныйКод = "Параметры.КнопкиКлавиатуры.Очистить();
								   |
								   |Если Параметры.ТекстСообщения = ""Ввести одноразовый пароль (OTP)"" Тогда
								   |
								   |Результат.Сообщение.Текст = ""Введите одноразовый пароль (OTP)"";
								   |
								   |ЗавершитьЭтап(Параметры.НаименованиеТекущегоЭтапа);
								   |
								   |ИначеЕсли ЗначениеЗаполнено(Параметры.Контакт.НомерТелефона) Тогда
								   |
								   |ОбъектЧата = Чат.ПолучитьОбъект();
								   |ОбъектЧата.ИмяКонтакта = Параметры.Контакт.Имя;
								   |ОбъектЧата.НомерТелефона = Параметры.Контакт.НомерТелефона;
								   |ОбъектЧата.Записать();
								   |
								   |Результат.Сообщение.Текст = ""Спасибо! Ваш запрос на авторизацию будет рассмотрен со стороны администратора."";
								   |
								   |ЗавершитьЭтап(Параметры.НаименованиеТекущегоЭтапа);
								   |ЗавершитьЭтап(""Ввод OTP"");
								   |
								   |Иначе
								   |
								   |КнопкаКлавиатуры = Справочники.УБТ_ОбщенияБотовTelegramСЧатами.НоваяСтруктураКнопкаКлавиатуры();
								   |КнопкаКлавиатуры.Наименование = ""Отправить номер телефона"";
								   |КнопкаКлавиатуры.ЗапросНомераТелефона = Истина;
								   |Параметры.КнопкиКлавиатуры.Добавить(КнопкаКлавиатуры);
								   |
								   |КнопкаКлавиатуры = Справочники.УБТ_ОбщенияБотовTelegramСЧатами.НоваяСтруктураКнопкаКлавиатуры();
								   |КнопкаКлавиатуры.Наименование = ""Ввести одноразовый пароль (OTP)"";
								   |Параметры.КнопкиКлавиатуры.Добавить(КнопкаКлавиатуры);
								   |
								   |Результат.Сообщение.Текст = ""Пожалуйста, выебрите один из доступных вариантов авторизации."";
								   |
								   |КонецЕсли;";

	ЭтапВводаOTP = СценарийАвторизации.Этапы.Добавить();
	ЭтапВводаOTP.НаименованиеЭтапа = "Ввод OTP";
	ЭтапВводаOTP.УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор);
	ЭтапВводаOTP.ПроизвольныйКод = "ДанныеПароля = РегистрыСведений.УБТ_ОдноразовыеПаролиАвторизацииЧатовTelegram.ОдноразовыйПарольАвторизацииЧата(ЗапрошенныйСценарий);
								   |
								   |Пароль = ДанныеПароля.Пароль;
								   |
								   |Если Параметры.ТекстСообщения = Пароль Тогда
								   |
								   |
								   |Запись = РегистрыСведений.УБТ_АвторизованныеСценарииЧатовTelegram.СоздатьМенеджерЗаписи();
								   |Запись.Бот = Бот;
								   |Запись.Чат = Чат;
								   |Запись.Сценарий = ЗапрошенныйСценарий;
								   |Запись.Записать();
								   |
								   |Результат.Сообщение.Текст = ""Авторизация успешно завершена. Вы можете продолжить работу с ботом"";
								   |
								   |ЗавершитьЭтап(Параметры.НаименованиеТекущегоЭтапа);
								   |
								   |Иначе
								   |
								   |Результат.Сообщение.Текст = ""Неверный пароль"";
								   |
								   |КонецЕсли;";

	СценарийАвторизации.Версия = АктуальнаяВерсияСценария;

	СценарийАвторизации.ОбменДанными.Загрузка = Истина;

	СценарийАвторизации.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СценарийАвторизацииЧатов() Экспорт

	ИмяСценарияАвторизации = ИмяСценарияАвторизации();

	Возврат НайтиПоНаименованию(ИмяСценарияАвторизации);

КонецФункции

Функция ИмяСценарияАвторизации() Экспорт

	Возврат "Authorization";

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВерсияСценарияАвторизации()

	Возврат 4;

КонецФункции

#КонецОбласти

#КонецЕсли