#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Управляет регламентным заданием сценария. Расписание задания задается в форме элемента.
// Предусматривает работу через подсистему "СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий".
//
// Параметры:
//  РасписаниеРегламентногоЗадания - РасписаниеРегламентногоЗадания - расписание регламентного задания.
//
Процедура ВключитьОтключитьРегламентноеЗадание(РасписаниеРегламентногоЗадания) Экспорт

	Задание = СуществующееЗадание();
	Если ИспользоватьРегламентноеЗадание Тогда

		ПараметрыЗадания = СвойстваЗадания(РасписаниеРегламентногоЗадания);
		ПользовательЗадания = ПользовательРегламентногоЗадания;

		Если ЗначениеЗаполнено(ПользовательЗадания) Тогда
			ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПользовательЗадания,
				"ИдентификаторПользователяИБ");
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
				ИдентификаторПользователяИБ);

			ПараметрыЗадания.Вставить("ИмяПользователя", ПользовательИБ.Имя);
		Иначе
			ПараметрыЗадания.Вставить("ИмяПользователя", "");
		КонецЕсли;

		Если Задание = Неопределено Тогда

			ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.УБТ_ИсполнениеСценариевTelegramБотов);
			ПараметрыЗадания.Вставить("Ключ", Строка(Новый УникальныйИдентификатор));
			ПараметрыЗадания.Вставить("Использование", Истина);

			ИдентификаторРегламентногоЗадания = НовоеЗадание(ПараметрыЗадания);
		Иначе
			УстановитьПараметрыЗадания(Задание, ПараметрыЗадания);
		КонецЕсли;

	Иначе

		Если Задание <> Неопределено Тогда
			УдалитьЗадание(Задание);
		КонецЕсли;
		ИдентификаторРегламентногоЗадания = Неопределено;

	КонецЕсли;

КонецПроцедуры

// Получение свойств регламентного задания.
// 
// Параметры:
//  РасписаниеЗадания - Неопределено, РасписаниеРегламентногоЗадания - Расписание регламентного задания
// 
// Возвращаемое значение:
//  Структура - Свойства задания:
// * Расписание - Неопределено, РасписаниеРегламентногоЗадания - Расписание
// * Параметры - Массив Из Строка 
// * ИмяМетода - Строка - 
// * Наименование - Строка - 
Функция СвойстваЗадания(РасписаниеЗадания = Неопределено) Экспорт

	Параметры = Новый Массив;
	Параметры.Добавить(Строка(Ссылка.УникальныйИдентификатор()));

	ПараметрыЗадания = Новый Структура;
	Если Не РасписаниеЗадания = Неопределено Тогда
		ПараметрыЗадания.Вставить("Расписание", РасписаниеЗадания);
	КонецЕсли;
	ПараметрыЗадания.Вставить("Параметры", Параметры);
	ПараметрыЗадания.Вставить("ИмяМетода",
		Метаданные.РегламентныеЗадания.УБТ_ИсполнениеСценариевTelegramБотов.ИмяМетода);
	ПараметрыЗадания.Вставить("Наименование", НаименованиеРегламентногоЗадания());

	Возврат ПараметрыЗадания;

КонецФункции

// Функция - Существующее задание
// 
// Возвращаемое значение:
//   РегламентноеЗадание - регламентное задание - регламентное задание, соответствующее сценарию.
//							Если задания нет - возвращается "Неопределено".
//
Функция СуществующееЗадание() Экспорт

	Отбор = Новый Структура;

	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("Ключ", ИдентификаторРегламентногоЗадания);
	Иначе
		Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда
			Отбор.Вставить("Идентификатор", Новый УникальныйИдентификатор(ИдентификаторРегламентногоЗадания));
		КонецЕсли;
		Отбор.Вставить("Наименование", НаименованиеРегламентногоЗадания());

	КонецЕсли;

	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.УБТ_ИсполнениеСценариевTelegramБотов);

	Найденные = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Задание = ?(Найденные.Количество() = 0, Неопределено, Найденные[0]);

	Возврат Задание;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ОчиститьНеиспользуемыеРеквизитыИТабличныеЧасти();

	Если Ссылка <> Справочники.УБТ_СценарииБотовTelegram.СценарийАвторизацииЧатов() Тогда
		ПроверяемыеРеквизиты.Добавить("Бот");
	КонецЕсли;

	Если ТипСценария = Перечисления.УБТ_ТипыСценариевБотовTelegram.ОтправкаОтчета Тогда
		ПроверяемыеРеквизиты.Добавить("Отчеты");
	ИначеЕсли ТипСценария = Перечисления.УБТ_ТипыСценариевБотовTelegram.ПроизвольныйСценарий Тогда
		ПроверяемыеРеквизиты.Добавить("ПроизвольныйКод");
	ИначеЕсли ТипСценария = Перечисления.УБТ_ТипыСценариевБотовTelegram.Общение Тогда
		ПроверяемыеРеквизиты.Добавить("Этапы");
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;

	Если Ссылка = Справочники.УБТ_СценарииБотовTelegram.СценарийАвторизацииЧатов() Тогда
		ТекстОшибки = НСтр(
			"az = 'Çat avtorizasiya skriptinin redaktə edilməsi qadağandır.';en = 'Editing the chat authorization script is forbidden';ru = 'Запрещено редактирование сценария авторизации чатов.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
	ИначеЕсли Наименование = Справочники.УБТ_СценарииБотовTelegram.ИмяСценарияАвторизации() Тогда
		ТекстОшибки = НСтр(
			"az = 'Ad artıq əvvəlcədən təyin edilmiş skript tərəfindən istifadə olunur. Zəhmət olmasa, başqa ad seçin.';en = 'The name is already used by a predefined script. Please choose another name.';ru = 'Наименование уже используется предопределенным сценарием. Выберите другое наименование.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Наименование", , Отказ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьНеиспользуемыеРеквизитыИТабличныеЧасти()

	Если ТипСценария <> Перечисления.УБТ_ТипыСценариевБотовTelegram.ОтправкаОтчета Тогда
		Отчеты.Очистить();
		РасширенияОтчетов.Очистить();
	КонецЕсли;

	Если ТипСценария <> Перечисления.УБТ_ТипыСценариевБотовTelegram.Общение Тогда
		Этапы.Очистить();
		КнопкиКлавиатуры.Очистить();
	КонецЕсли;

	Если ТипСценария <> Перечисления.УБТ_ТипыСценариевБотовTelegram.ПроизвольныйСценарий Тогда
		ПроизвольныйКод = "";
	КонецЕсли;

КонецПроцедуры

Функция НаименованиеРегламентногоЗадания()

	Возврат СтрШаблон(НСтр(
		"az = 'Telegram bot skripti: %1';en = 'Telegram bot script: %1';ru = 'Сценарий Telegram бота: %1'",
		ОбщегоНазначения.КодОсновногоЯзыка()), Наименование);

КонецФункции

// Удаляет регламентное задание, соответствующее объекту.
//
// Параметры:
// Задание - ОбъектМетаданных
// 
Процедура УдалитьЗадание(Задание)

	РегламентныеЗаданияСервер.УдалитьЗадание(Задание);

КонецПроцедуры

Функция НовоеЗадание(ПараметрыЗадания)

	РегламентноеЗадание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);

	Если ТипЗнч(РегламентноеЗадание) = Тип("СтрокаТаблицыЗначений") Тогда
		Идентификатор = РегламентноеЗадание.Ключ;
	Иначе
		Идентификатор = Строка(РегламентноеЗадание.УникальныйИдентификатор);
	КонецЕсли;

	Возврат Идентификатор;

КонецФункции

Процедура УстановитьПараметрыЗадания(Задание, СвойстваЗадания)

	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, СвойстваЗадания);

	УстановитьПривилегированныйРежим(Истина);

КонецПроцедуры

#КонецОбласти

#КонецЕсли