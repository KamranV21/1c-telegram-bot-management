#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает GUID бота с префиксом расширения. Используется для совместимости с конфигурациями и платформами нижних версий.
// 
// Параметры:
//  Бот - СправочникСсылка.УБТ_БотыTelegram - Бот
// 
// Возвращаемое значение:
//  Строка - Идентификатор элемента бота с префиксом
Функция ИдентификаторЭлементаБотаСПрефиксом(Бот) Экспорт

	Возврат "УБТ_Бот_" + Строка(Бот.УникальныйИдентификатор());

КонецФункции

// Возвращает бота Telegram по имени.
// 
// Параметры:
//  ИмяБота - Строка - Имя бота
// 
// Возвращаемое значение:
//  СправочникСсылка.УБТ_БотыTelegram - Бот Telegram
Функция НайтиБотаTelegramПоИмени(ИмяБота) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	УБТ_БотыTelegram.Ссылка КАК Бот
						  |ИЗ
						  |	Справочник.УБТ_БотыTelegram КАК УБТ_БотыTelegram
						  |ГДЕ
						  |	УБТ_БотыTelegram.Код = &ИмяБота
						  |	И НЕ УБТ_БотыTelegram.ПометкаУдаления");
	Запрос.УстановитьПараметр("ИмяБота", ИмяБота);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Бот;
	КонецЕсли;

	Возврат ПустаяСсылка();

КонецФункции

// Возвращает бота Telegram по секретному заголовку.
// 
// Параметры:
//  ИмяБота - Строка - Имя бота
//  СекретВебхука - Строка - Секрет бота
// 
// Возвращаемое значение:
//  СправочникСсылка.УБТ_БотыTelegram - Бот Telegram
Функция НайтиБотаTelegramПоИмениИСекретномуЗаголовку(ИмяБота, СекретВебхука) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	УБТ_БотыTelegram.Ссылка КАК Бот
						  |ИЗ
						  |	Справочник.УБТ_БотыTelegram КАК УБТ_БотыTelegram
						  |ГДЕ
						  |	УБТ_БотыTelegram.ВариантВзаимодействия = ЗНАЧЕНИЕ(Перечисление.УБТ_ВариантыВзаимодействияСБотамиTelegram.Вебхук)
						  |	И УБТ_БотыTelegram.СекретВебхука = &СекретВебхука
						  |	И НЕ УБТ_БотыTelegram.ПометкаУдаления
						  |	И УБТ_БотыTelegram.Код = &ИмяБота");
	Запрос.УстановитьПараметр("ИмяБота", ИмяБота);
	Запрос.УстановитьПараметр("СекретВебхука", СекретВебхука);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Бот;
	КонецЕсли;

	Возврат ПустаяСсылка();

КонецФункции

// Возвращает список активных ботов Telegram.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.УБТ_БотыTelegram - Активные боты Telegram
Функция АктивныеБотыTelegram() Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
						  |	УБТ_БотыTelegram.Ссылка КАК Бот
						  |ИЗ
						  |	Справочник.УБТ_БотыTelegram КАК УБТ_БотыTelegram
						  |ГДЕ
						  |	НЕ УБТ_БотыTelegram.ПометкаУдаления");

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Бот");

КонецФункции

#КонецОбласти

#КонецЕсли