#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Чаты бота по идентификаторам.
// 
// Параметры:
//  Бот - СправочникСсылка.УБТ_БотыTelegram - Бот
//  ИдентификаторыЧатов - Массив Из Строка - Идентификаторы чатов
// 
// Возвращаемое значение:
//  РезультатЗапроса - Чаты по идентификаторам
Функция ЧатыПоИдентификаторам(Бот, ИдентификаторыЧатов) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	УБТ_ЧатыTelegram.Ссылка КАК Чат
						  |ИЗ
						  |	Справочник.УБТ_ЧатыTelegram КАК УБТ_ЧатыTelegram
						  |ГДЕ
						  |	УБТ_ЧатыTelegram.Код В (&ИдентификаторыЧатов)
						  |	И УБТ_ЧатыTelegram.Владелец = &Бот
						  |	И НЕ УБТ_ЧатыTelegram.ПометкаУдаления");
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ИдентификаторыЧатов", ИдентификаторыЧатов);

	Возврат Запрос.Выполнить();

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Представление = СтрШаблон("%1 (%2)", Данные.Наименование, Данные.Код);

КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Поля.Добавить("Наименование");
	Поля.Добавить("Код");

КонецПроцедуры

#КонецОбласти

#КонецЕсли