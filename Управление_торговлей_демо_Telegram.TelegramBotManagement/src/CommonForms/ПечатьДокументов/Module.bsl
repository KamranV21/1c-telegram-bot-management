
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура УБТ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	УБТ_РазместитьКнопкуОтправкиВTelegram();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УБТ_РазместитьКнопкуОтправкиВTelegram()

	КомандаTelegram = Команды.Добавить("УБТ_ОтправитьПоTelegram");
	КомандаTelegram.Заголовок = НСтр(
		"az = 'Telegram ilə göndərmək';en = 'Send via Telegram';ru = 'Отправить по Telegram'");
	КомандаTelegram.Подсказка = НСтр(
		"az = 'Telegram ilə göndərmək';en = 'Send via Telegram';ru = 'Отправить по Telegram'");
	КомандаTelegram.Действие = "УБТ_ОтправитьПоTelegram";
	КомандаTelegram.Картинка = БиблиотекаКартинок.УБТ_Telegram16;
	КомандаTelegram.Отображение = ОтображениеКнопки.КартинкаИТекст;

	Кнопка = Элементы.Вставить("УБТ_ОтправитьПоTelegram", Тип("КнопкаФормы"), Элементы.ГруппаСохранитьОтправить,
		Элементы.КнопкаОтправить);
	Кнопка.ИмяКоманды = "УБТ_ОтправитьПоTelegram";

КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура УБТ_ОтправитьПоTelegram(Команда)

	УБТ_ОтправитьПечатнуюФормуПоTelegram();

КонецПроцедуры

&Перед("ОтправитьПечатныеФормыПоПочте")
&НаКлиенте
Процедура УБТ_ОтправитьПечатныеФормыПоПочте()
	
	ПараметрыВывода.Вставить("УБТ_ВидЭлектронногоСообщения", ПредопределенноеЗначение(
		"Перечисление.УБТ_ВидыЭлектронныхСообщений.ЭлектронноеПисьмо"));
	
КонецПроцедуры

&НаКлиенте
Процедура УБТ_ОтправитьПечатнуюФормуПоTelegram()

	ПараметрыВывода.Вставить("УБТ_ВидЭлектронногоСообщения", ПредопределенноеЗначение(
		"Перечисление.УБТ_ВидыЭлектронныхСообщений.Telegram"));

	ПараметрыФормы = ОбщегоНазначенияСлужебныйКлиент.НастройкиФорматаПечатнойФормы();
	ИмяОткрываемойФормы = "ОбщаяФорма.ВыборФорматаВложений";
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия")
		И СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ИспользоватьПочтовыйКлиент Тогда
		Если ПолучателейБольшеОдного(ПараметрыВывода.ПараметрыОтправки.Получатель) Тогда
			ПараметрыФормы.Вставить("Получатели", ПараметрыВывода.ПараметрыОтправки.Получатель);
			ИмяОткрываемойФормы = "ОбщаяФорма.ПодготовкаНовогоПисьма";
		КонецЕсли;
	КонецЕсли;

	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти