#Область ОбработчикиСобытийФормы

&НаСервере
Процедура УБТ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	УБТ_РазместитьКнопкуОтправкиВTelegram();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УБТ_РазместитьКнопкуОтправкиВTelegram()

	КомандаTelegram = Команды.Добавить("УБТ_ОтправитьПоTelegram");
	КомандаTelegram.Заголовок = НСтр(
		"az = 'Telegram ilə göndərmək';en = 'Send via Telegram';ru = 'Отправить по Telegram'");
	КомандаTelegram.Подсказка = НСтр(
		"az = 'Telegram ilə göndərmək';en = 'Send via Telegram';ru = 'Отправить по Telegram'");
	КомандаTelegram.Действие = "УБТ_ОтправитьПоTelegram";
	КомандаTelegram.Картинка = БиблиотекаКартинок.УБТ_Telegram16;
	КомандаTelegram.Отображение = ОтображениеКнопки.КартинкаИТекст;

	Кнопка = Элементы.Добавить("УБТ_ОтправитьПоTelegram", Тип("КнопкаФормы"), Элементы.ГруппаСохранениеИРассылкаВарианта);
	Кнопка.ИмяКоманды = "УБТ_ОтправитьПоTelegram";

КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура УБТ_ОтправитьПоTelegram(Команда)

	ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
	Если ОтображениеСостояния.Видимость = Истина И ОтображениеСостояния.ДополнительныйРежимОтображения
		= ДополнительныйРежимОтображения.Неактуальность Тогда
		ТекстВопроса = НСтр(
			"az = 'Hesabat yaradılmayıb. Yaratmaq?';en = 'Report not generated. Generate?';ru = 'Отчет не сформирован. Сформировать?'");
		Обработчик = Новый ОписаниеОповещения("УБТ_СформироватьПередОтправкойПоTelegram", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		УБТ_ОтправитьПечатнуюФормуПоTelegram();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УБТ_СформироватьПередОтправкойПоTelegram(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Обработчик = Новый ОписаниеОповещения("УБТ_ОтправитьПоTelegramПослеФормирования", ЭтотОбъект);
		ОтчетыКлиент.СформироватьОтчет(ЭтотОбъект, Обработчик);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УБТ_ОтправитьПоTelegramПослеФормирования(ТабличныйДокументСформирован, ДополнительныеПараметры) Экспорт
	Если ТабличныйДокументСформирован Тогда
		УБТ_ОтправитьПечатнуюФормуПоTelegram();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УБТ_ОтправитьПечатнуюФормуПоTelegram()

	Вложение = Новый Структура;
	Вложение.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилище(ОтчетТабличныйДокумент,
		УникальныйИдентификатор));
	Вложение.Вставить("Представление", ОтчетНаименованиеТекущегоВарианта);

	СписокВложений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Вложение);

	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда

		МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"РаботаСПочтовымиСообщениямиКлиент");

		ПараметрыОтправки = МодульРаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
		ПараметрыОтправки.Тема = ОтчетНаименованиеТекущегоВарианта;
		ПараметрыОтправки.Вложения = СписокВложений;
		ПараметрыОтправки.Вставить("ОповещениеОЗакрытииФормы", Неопределено);

		ПараметрыОтправки.Вставить("УБТ_ВидЭлектронногоСообщения", ПредопределенноеЗначение(
		"Перечисление.УБТ_ВидыЭлектронныхСообщений.Telegram"));

		СведенияДляОтправки = РаботаСПочтовымиСообщениямиВызовСервера.СведенияДляОтправки(ПараметрыОтправки);
		ПараметрыОтправки.Вставить("ПоказыватьДиалогВыбораФорматаСохраненияВложений",
			СведенияДляОтправки.ПоказыватьДиалогВыбораФорматаСохраненияВложений);

		МодульРаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмоПроверкаУчетнойЗаписиВыполнена(Истина,
			ПараметрыОтправки);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти