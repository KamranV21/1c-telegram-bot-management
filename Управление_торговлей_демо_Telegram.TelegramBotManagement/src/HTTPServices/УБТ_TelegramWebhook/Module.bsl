#Область ОбработчикиСобытий

Функция WebhookPOST(HTTPЗапрос)

	СекретВебхука = HTTPЗапрос.Заголовки.Получить("X-Telegram-Bot-Api-Secret-Token");
	Если Не ЗначениеЗаполнено(СекретВебхука) Тогда
		СекретВебхука = "";
	КонецЕсли;

	ИмяБота = HTTPЗапрос.ПараметрыURL.Получить("bot");
	Бот = Справочники.УБТ_БотыTelegram.НайтиБотаTelegramПоИмениИСекретномуЗаголовку(ИмяБота, СекретВебхука);

	Если Не ЗначениеЗаполнено(Бот) Тогда
		Возврат Новый HTTPСервисОтвет(404);
	КонецЕсли;

	ТелоЗапроса = HTTPЗапрос.ПолучитьТелоКакСтроку();
	Обновление = УБТ_РаботаСJSON.JSONВЗначение(ТелоЗапроса, , Ложь);
	МассивОбновлений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Обновление);

	ТаблицаСообщений = УБТ_РаботаСБотамиTelegram.ПолученныеСообщенияВТаблицуЗначений(МассивОбновлений);
	УБТ_РаботаСБотамиTelegram.ОтветитьНаПолученныеСообщения(Бот, ТаблицаСообщений);

	HTTPОтвет = Новый HTTPСервисОтвет(200);
	Возврат HTTPОтвет;

КонецФункции

#КонецОбласти